buildscript {
  repositories { jcenter() }

  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.+'
    classpath 'me.tatarka:gradle-retrolambda:1.3.+'
  }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'retrolambda'

ext.base_version = '0.1.1'
ext.mc_version = '14w31a'

archivesBaseName = 'vanilla-irc'
group = 'com.minecraftonline'
version = "${base_version}-${mc_version}"

repositories {
  mavenCentral()
}

task downloadMinecraft {
  outputs.file "lib/minecraft_server.${mc_version}.jar"
  
  doLast {
    outputs.files.singleFile.parentFile.mkdirs()
    ant.get src: "https://s3.amazonaws.com/Minecraft.Download/versions/${mc_version}/minecraft_server.${mc_version}.jar", dest: "lib", verbose: true
  }
}

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
}

dependencies {
  compile 'org.pircbotx:pircbotx:2.0.1'
  compile files("lib/minecraft_server.${mc_version}.jar") {
    builtBy downloadMinecraft
  }

  provided 'org.projectlombok:lombok:1.14.+'

  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
}

jar {
  manifest {
    attributes 'Class-Path': "minecraft_server.${mc_version}.jar",
               'Main-Class': 'com.minecraftonline.vanillairc.VanillaIRC',
               'Implementation-Title': rootProject.name,
               'Implementation-Version': version,
               'Implementation-Vendor': '14mRh4X0r'
  }
}

assemble.dependsOn shadowJar
