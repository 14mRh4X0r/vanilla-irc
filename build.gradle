plugins {
  id "com.github.johnrengelman.shadow" version "1.2.2"
  id "me.tatarka.retrolambda" version "3.2.2"
}

apply plugin: 'java'

ext.base_version = '0.2.1'
ext.mc_version = '16w21b'

archivesBaseName = 'vanilla-irc'
group = 'com.minecraftonline'
version = "${base_version}-${mc_version}"

repositories {
  mavenCentral()
}

task downloadMinecraft {
  outputs.file "lib/minecraft_server.${mc_version}.jar"
  
  doLast {
    outputs.files.singleFile.parentFile.mkdirs()
    ant.get src: "https://s3.amazonaws.com/Minecraft.Download/versions/${mc_version}/minecraft_server.${mc_version}.jar", dest: "lib", verbose: true
  }
}

configurations {
  provided
}

sourceSets {
  main {
    compileClasspath += configurations.provided
  }
}

dependencies {
  compile 'org.pircbotx:pircbotx:2.0.1'
  compile files("lib/minecraft_server.${mc_version}.jar") {
    builtBy downloadMinecraft
  }

  provided 'org.projectlombok:lombok:1.16.+'

  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'
}

jar {
  manifest {
    attributes 'Class-Path': "minecraft_server.${mc_version}.jar",
               'Main-Class': 'com.minecraftonline.vanillairc.VanillaIRC',
               'Implementation-Title': rootProject.name,
               'Implementation-Version': version,
               'Implementation-Vendor': '14mRh4X0r'
  }
}

task checkClass {
  check.dependsOn checkClass
  onlyIf { tasks.compileJava.didWork }

  doLast {
    def nmsFile = sourceSets.main.java.files.getAt(0)
    def nmsName = org.apache.commons.io.FilenameUtils.getBaseName(nmsFile.name)
    def nmsCheck = /ObfuscationHelper\.setLoginManager\(this, ${nmsName}\.class\);/

    if (!(nmsFile.text =~ nmsCheck).find())
      throw new RuntimeException("Wrong class name in ${nmsFile}, aborting")
  }
}

assemble.dependsOn clean, shadowJar
